apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())


repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        standard {
            java.srcDirs('src/common/java')
        }
    }

    compileSdkVersion 33

    defaultConfig {
        applicationId "edu.cmu.cs.openfluid"
        targetSdkVersion 33
        multiDexEnabled true
        signingConfig signingConfigs.debug
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        customDebugType {
            debuggable true
        }
    }

    buildFeatures {
        dataBinding true
    }

    flavorDimensions "version"
    productFlavors {
        standard {
            isDefault = true
            minSdkVersion 24
        }
    }

    // We can't use protoc:3.8.0 because MobileEdgeX uses an old version of protobuf
    protobuf {
        protoc {
            // You still need protoc like in the non-Android case
            artifact = 'com.google.protobuf:protoc:3.7.0'
        }
        plugins {
            javalite {
                // The codegen for lite comes as a separate artifact
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    // In most cases you don't need the full Java output
                    // if you use the lite output.
                    remove java
                }
                task.plugins {
                    javalite {
                        outputSubDir = 'java'
                    }
                }
            }
        }
    }
    namespace 'edu.cmu.cs.openfluid'
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'edu.cmu.cs.gabriel:client:2.3.0'
    implementation 'edu.cmu.cs.gabriel:camera:2.3.0'
    implementation 'edu.cmu.cs.gabriel:serverlist:2.3.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation project(':sinfonia')

    standardImplementation 'edu.cmu.cs.gabriel:protocol-old-protobuf:2.0.1'
}
